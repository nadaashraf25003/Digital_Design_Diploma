`timescale 1ns / 1ps

module uart_loopback_wrapper (
    input  wire        rx_clk,        // UART RX sampling clock (baud x Prescale)
    input  wire        RST_N,         // KEY[0] active-low reset
    // RX side
    input wire         uart_rx,       // UART RX serial input (Arduino D0 / hd_rx)
    // TX side
    output wire        uart_tx,       // UART TX serial output (Arduino D1 / hd_tx)
    output wire        TX_OUT_V,      // TX busy
    // Control
    input  wire        parity_enable, // 1=parity on, 0=off
    input  wire        parity_type,   // 0=even, 1=odd
    output wire        parity_error,  // LEDR[0]
    output wire        framing_error  // LEDR[1]
);

    // Hardwired baud divisor: Prescale = 32 decimal (6'b100000).
    // baud = rx_clk / Prescale.
    localparam [5:0] PRESCALE = 6'd32;

    wire        rst_sync;
    wire        rst_sync_tx;
    (* keep = "true" *) wire [7:0]  rx_out_p_i;   // RX parallel data (tap)
    (* keep = "true" *) wire        rx_out_v_i;   // RX data valid pulse (tap)
    wire        tx_clk;

    (* keep = "true" *) wire [7:0]  tx_data_p;    // ACK-domain data -> UART TX input (tap)
    wire        valid_level;  // ACK-domain held Data_Valid -> UART TX

    // ---------------------------------------------------------------
    // Reset: KEY[0] (active-low) synchronized into each clock domain.
    // ---------------------------------------------------------------
    RST_SYNC #(.NUM_STAGES(2)) U0_RST_SYNC (
        .RST     (RST_N),
        .CLK     (rx_clk),
        .SYNC_RST(rst_sync)
    );

    RST_SYNC #(.NUM_STAGES(2)) U2_RST_SYNC (
        .RST     (RST_N),
        .CLK     (tx_clk),
        .SYNC_RST(rst_sync_tx)
    );

    // ---------------------------------------------------------------
    // TX bit clock generator: tx_clk = rx_clk / 32 (one UART bit period per
    // 32 rx_clk cycles -> baud = rx_clk / Prescale). Synchronous with rx_clk
    // because tx_clk is derived from it by this divider, so the TX and RX
    // domains share the same source clock.
    // ---------------------------------------------------------------
    ClkDiv U0_ClkDiv_TX (
        .i_ref_clk  (rx_clk),
        .i_rst      (rst_sync),
        .i_clk_en   (1'b1),
        .i_div_ratio(8'd32),
        .o_div_clk  (tx_clk)
    );

    // Drive the divided clock on the global clock network so it is routed and
    // modeled as a proper clock at a stable node (U0_TX_CLKCTRL|outclk).



    // ---------------------------------------------------------------
    // ACK = UART TX busy level directly. The TX FSM accepts the byte when
    // busy rises; the module releases OUT_VLD while ACK (busy) is high. No
    // rising-edge detector is required because the module only presents data
    // while ACK is low (consumer idle).
    // ---------------------------------------------------------------
    REQ_ACK_SYNC #(
        .DATA_WIDTH(8)
    ) U0_REQ_ACK_SYNC (
        .REQ_CLK  (rx_clk),
        .RST_REQ  (rst_sync),
        .REQ      (rx_out_v_i),
        .REQ_DATA (rx_out_p_i),

        .ACK_CLK  (tx_clk),
        .RST_ACK  (rst_sync_tx),
        .ACK      (TX_OUT_V),
        .OUT_VLD  (valid_level),
        .OUT_DATA (tx_data_p)
    );

    // ---------------------------------------------------------------
    // UART - internal loopback: TX_IN* fed from the internal RX outputs.
    // ---------------------------------------------------------------
    UART #(
        .DATA_WIDTH(8)
    ) U_UART (
        .RST          (rst_sync),
        .TX_CLK       (tx_clk),
        .RX_CLK       (rx_clk),
        .RX_IN_S      (uart_rx),
        .RX_OUT_P     (rx_out_p_i),
        .RX_OUT_V     (rx_out_v_i),
        .TX_IN_P      (tx_data_p),
        .TX_IN_V      (valid_level),
        .TX_OUT_S     (uart_tx),
        .TX_OUT_V     (TX_OUT_V),
        .Prescale     (PRESCALE),
        .parity_enable(parity_enable),
        .parity_type  (parity_type),
        .parity_error (parity_error),
        .framing_error(framing_error)
    );

endmodule